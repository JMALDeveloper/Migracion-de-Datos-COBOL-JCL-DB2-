      ******************************************************************
      *       I D E N T I F I C A T I O N   D I V I S I O N
      *
      * PROGRAMA PARA CRUZAR FICHEROS: DES.UNLOAD.ENT.COMPANYA.F220519
      *                              & DES.UNLOAD.ENT.SEGUROS.F220519
      ******************************************************************
       IDENTIFICATION DIVISION.
      *
       PROGRAM-ID.    VALSNMAP.
       AUTHOR.        JOAQUIN ALPANYEZ LOPEZ.
       DATE-WRITTEN.  12/09/2023.
       DATE-COMPILED. 12/09/2023.
      *
      ******************************************************************
      *       E N V I R O N M E N T     D I V I S I O N
      ******************************************************************
       ENVIRONMENT DIVISION.
      *
       CONFIGURATION SECTION.
         SOURCE-COMPUTER.  IBM-3090.
         OBJECT-COMPUTER.  IBM-3090.
      *
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT FENTRADA
               ASSIGN TO ENTRADA
               FILE STATUS IS FS-FENTRADA.
      *
           SELECT FENTRADA2
               ASSIGN TO ENTRADA2
               FILE STATUS IS FS-FENTRADA2.
      *
           SELECT FSALIDA
               ASSIGN TO SALIDA
               FILE STATUS IS FS-FSALIDA.
      *
           SELECT FSALIDA2
               ASSIGN TO SALIDA2
               FILE STATUS IS FS-FSALIDA2.
      *
      ******************************************************************
      *                D A T A      D I V I S I O N
      ******************************************************************
       DATA DIVISION.
      *
       FILE SECTION.
      *
      ******************************************************************
      *        F I L E     S E C T I O N
      ******************************************************************
      *
       FD  FENTRADA
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORD ARE STANDARD
           RECORDING MODE IS F
           DATA RECORD IS REG-ENT.

       01  REG-ENT            PIC X(73).
      *
       FD  FENTRADA2
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORD ARE STANDARD
           RECORDING MODE IS F
           DATA RECORD IS REG-ENT2.

       01  REG-ENT2           PIC X(550).
      *
       FD  FSALIDA
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORD ARE STANDARD
           RECORDING MODE IS F
           DATA RECORD IS REG-SAL.

       01  REG-SAL            PIC X(614).
      *
       FD  FSALIDA2
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORD ARE STANDARD
           RECORDING MODE IS F
           DATA RECORD IS REG-SAL2.
       01  REG-SAL2           PIC X(614).
      *
      ******************************************************************
      *        W O R K I N G    S T O R A G E
      ******************************************************************
       WORKING-STORAGE SECTION.
      **************************-CONSTANTES-****************************
       01 CA-CONSTANTES-ALF.
          05 CA-PGM                       PIC X(08) VALUE 'VALSNMAP'.
          05 CA-00                        PIC X(02) VALUE '00'.
          05 CA-10                        PIC X(02) VALUE '10'.
      *
       01 CN-CONSTANTES-NUM.
          05 CN-1                         PIC 9(01) VALUE 1.
      ************************-VARIABLES-*******************************
       01 WK-VARIABLES.
          05 WK-LEIDOS                    PIC 9(03) VALUE 000.
          05 WK-LEIDOS2                   PIC 9(03) VALUE 000.
          05 WK-ESCRITOS                  PIC 9(03) VALUE 000.
          05 WK-ESCRITOS2                 PIC 9(03) VALUE 000.
      *
       01 FS-FILE-STATUS.
          05 FS-FENTRADA                  PIC X(02).
          05 FS-FENTRADA2                 PIC X(02).
          05 FS-FSALIDA                   PIC X(02).
          05 FS-FSALIDA2                  PIC X(02).
      *
      ********************-ESTRUCTURAS-*********************************
       01 CPYENT1.
          05 ID-E1                       PIC S9(9).
          05 NUMERO-POLIZA-E1            PIC X(09).
          05 NOMBRE-COMPANYA-E1          PIC X(55).
      *
       01 CPYENT2.
          05 NUMERO-POLIZA-E2           PIC X(09).
          05 TIPO-E2                    PIC X(02).
          05 FECHA-INICIO-E2            PIC X(10).
          05 FECHA-VENCIMIENTO-E2       PIC X(10).
          05 COND-PART-E2               PIC X(255).
          05 OBSERVACIONES-E2           PIC X(255).
          05 DNI-CL-E2                  PIC X(09).
      *
       01 CPYSAL1.
          05 ID-S1                   PIC S9(9).
          05 NUMERO-POLIZA-S1        PIC X(09).
          05 NOMBRE-COMPANYA-S1      PIC X(55).
          05 TIPO-S1                 PIC X(02).
          05 FECHA-INICIO-S1         PIC X(10).
          05 FECHA-VENCIMIENTO-S1    PIC X(10).
          05 COND-PART-S1            PIC X(255).
          05 OBSERVACIONES-S1        PIC X(255).
          05 DNI-CL-S1               PIC X(09).
      *
       01 CPYSAL2.
          05 ID-S2                   PIC S9(9).
          05 NUMERO-POLIZA-S2        PIC X(09).
          05 NOMBRE-COMPANYA-S2      PIC X(55).
          05 TIPO-S2                 PIC X(02).
          05 FECHA-INICIO-S2         PIC X(10).
          05 FECHA-VENCIMIENTO-S2    PIC X(10).
          05 COND-PART-S2            PIC X(255).
          05 OBSERVACIONES-S2        PIC X(255).
          05 DNI-CL-S2               PIC X(09).
      *
      ************************-SWITCHES-********************************
       01 SWITCHES.
          05 SW-ERROR                     PIC X(01) VALUE 'N'.
             88 SI-ERROR                            VALUE 'S'.
             88 NO-ERROR                            VALUE 'N'.
      *
          05 SW-FIN-FICHERO               PIC X(01) VALUE 'N'.
             88 SI-FIN-FICHERO1                     VALUE 'S'.
             88 SI-FIN-FICHERO2                     VALUE 'S'.
             88 NO-FIN-FICHERO1                     VALUE 'N'.
             88 NO-FIN-FICHERO2                     VALUE 'N'.
      *
      ******************************************************************
      *        P R O C E D U R E     D I V I S I O N
      ******************************************************************
       PROCEDURE DIVISION.
      *
           PERFORM 1000-INICIO
              THRU 1000-INICIO-EXIT
      *
           PERFORM 2000-PROCESO
              THRU 2000-PROCESO-EXIT
             UNTIL SI-FIN-FICHERO1 AND SI-FIN-FICHERO2
      *
           PERFORM 3000-FIN
              THRU 3000-FIN-EXIT.
      *
      ******************************************************************
      *   1000-INICIO
      ******************************************************************
       1000-INICIO.
      *
           INITIALIZE WK-VARIABLES
                      FS-FILE-STATUS
                      CPYENT1
                      CPYENT2
                      CPYSAL1
                      CPYSAL2
      *
           SET NO-ERROR              TO TRUE
           SET NO-FIN-FICHERO1       TO TRUE
           SET NO-FIN-FICHERO2       TO TRUE
      *
           PERFORM 1050-ABRIR-ENTRADA
              THRU 1050-ABRIR-ENTRADA-EXIT
      *
           PERFORM 1100-ABRIR-SALIDA
              THRU 1100-ABRIR-SALIDA-EXIT
      *
           PERFORM 9000-LEER-ENTRADA-COMP
              THRU 9000-LEER-ENTRADA-COMP-EXIT
      *
           PERFORM 9100-LEER-ENTRADA-SEG
              THRU 9100-LEER-ENTRADA-SEG-EXIT
      *
           .
       1000-INICIO-EXIT.
           EXIT.
      ******************************************************************
      *   1050-ABRIR-ENTRADA
      ******************************************************************
       1050-ABRIR-ENTRADA.
      *
           OPEN INPUT FENTRADA
      *
           IF FS-FENTRADA = CA-00
              CONTINUE
           ELSE
              SET SI-ERROR          TO TRUE
      *
              PERFORM 3000-FIN
                 THRU 3000-FIN-EXIT
      *
           END-IF
      *
           OPEN INPUT FENTRADA2
      *
           IF FS-FENTRADA2 = CA-00
              CONTINUE
           ELSE
              SET SI-ERROR          TO TRUE
      *
              PERFORM 3000-FIN
                 THRU 3000-FIN-EXIT
      *
           END-IF
      *
           .
       1050-ABRIR-ENTRADA-EXIT.
           EXIT.
      ******************************************************************
      *   1100-ABRIR-SALIDA
      ******************************************************************
       1100-ABRIR-SALIDA.
      *
           OPEN OUTPUT FSALIDA
      *
           IF FS-FSALIDA = CA-00
              CONTINUE
           ELSE
              SET SI-ERROR          TO TRUE
      *
              PERFORM 3000-FIN
                 THRU 3000-FIN-EXIT
      *
           END-IF
      *
           OPEN OUTPUT FSALIDA2
      *
           IF FS-FSALIDA2 = CA-00
              CONTINUE
           ELSE
              SET SI-ERROR          TO TRUE
      *
              PERFORM 3000-FIN
                 THRU 3000-FIN-EXIT
      *
           END-IF
           .
       1100-ABRIR-SALIDA-EXIT.
           EXIT.
      ******************************************************************
      *   2000-PROCESO
      ******************************************************************
       2000-PROCESO.
      *
                 PERFORM 2700-VALIDAR-MAPFRE
                    THRU 2700-VALIDAR-MAPFRE-EXIT
      *
           .
       2000-PROCESO-EXIT.
           EXIT.
      ******************************************************************
       2700-VALIDAR-MAPFRE.
      *
                 IF NUMERO-POLIZA-E1 = NUMERO-POLIZA-E2
      *
                   PERFORM 2500-INFORMAR-SALIDA1
                      THRU 2500-INFORMAR-SALIDA1-EXIT
      *
                   PERFORM 2300-ESCRIBIR-SALIDA
                      THRU 2300-ESCRIBIR-SALIDA-EXIT
      *
                   PERFORM 9000-LEER-ENTRADA-COMP
                      THRU 9000-LEER-ENTRADA-COMP-EXIT
      *
                   PERFORM 9100-LEER-ENTRADA-SEG
                      THRU 9100-LEER-ENTRADA-SEG-EXIT
      *
                 ELSE
      *
                     IF NUMERO-POLIZA-E1 < NUMERO-POLIZA-E2
      *
                        PERFORM 2600-INFORMAR-SALIDA2
                           THRU 2600-INFORMAR-SALIDA2-EXIT
      *
                        PERFORM 2450-ESCRIBIR-SALIDA2
                           THRU 2450-ESCRIBIR-SALIDA2-EXIT
      *
                        PERFORM 9000-LEER-ENTRADA-COMP
                           THRU 9000-LEER-ENTRADA-COMP-EXIT
      *
                     ELSE
      *
                        PERFORM 2600-INFORMAR-SALIDA2
                           THRU 2600-INFORMAR-SALIDA2-EXIT
      *
                        PERFORM 2450-ESCRIBIR-SALIDA2
                           THRU 2450-ESCRIBIR-SALIDA2-EXIT
      *
                        PERFORM 9100-LEER-ENTRADA-SEG
                           THRU 9100-LEER-ENTRADA-SEG-EXIT
      *
                     END-IF
                END-IF
      *
              .
       2700-VALIDAR-MAPFRE-EXIT.
             EXIT.
      ******************************************************************
       2500-INFORMAR-SALIDA1.
      *
             MOVE ID-E1                TO ID-S1
             MOVE NUMERO-POLIZA-E1     TO NUMERO-POLIZA-S1
             MOVE NOMBRE-COMPANYA-E1   TO NOMBRE-COMPANYA-S1
             MOVE TIPO-E2              TO TIPO-S1
             MOVE FECHA-INICIO-E2      TO FECHA-INICIO-S1
             MOVE FECHA-VENCIMIENTO-E2 TO FECHA-VENCIMIENTO-S1
             MOVE COND-PART-E2         TO COND-PART-S1
             MOVE OBSERVACIONES-E2     TO OBSERVACIONES-S1
             MOVE DNI-CL-E2            TO DNI-CL-S1
      *
               .
       2500-INFORMAR-SALIDA1-EXIT.
                EXIT.
      ******************************************************************
       2600-INFORMAR-SALIDA2.
      *
             MOVE ID-E1                TO ID-S2
             MOVE NUMERO-POLIZA-E2     TO NUMERO-POLIZA-S2
             MOVE NOMBRE-COMPANYA-E1   TO NOMBRE-COMPANYA-S2
             MOVE TIPO-E2              TO TIPO-S2
             MOVE FECHA-INICIO-E2      TO FECHA-INICIO-S2
             MOVE FECHA-VENCIMIENTO-E2 TO FECHA-VENCIMIENTO-S2
             MOVE COND-PART-E2         TO COND-PART-S2
             MOVE OBSERVACIONES-E2     TO OBSERVACIONES-S2
             MOVE DNI-CL-E2            TO DNI-CL-S2
      *
               .
       2600-INFORMAR-SALIDA2-EXIT.
                EXIT.
      ******************************************************************
      *   2300-ESCRIBIR-SALIDA
      ******************************************************************
       2300-ESCRIBIR-SALIDA.
      *
           WRITE REG-SAL FROM CPYSAL1
      *
           IF FS-FSALIDA = CA-00
              ADD CN-1           TO WK-ESCRITOS
              INITIALIZE CPYSAL1
           ELSE
              SET SI-ERROR       TO TRUE

              PERFORM 3000-FIN
                 THRU 3000-FIN-EXIT
           END-IF
      *
           .
      *
       2300-ESCRIBIR-SALIDA-EXIT.
           EXIT.
      ******************************************************************
       2450-ESCRIBIR-SALIDA2.
      *
           WRITE REG-SAL2 FROM CPYSAL2
      *
           IF FS-FSALIDA2 = CA-00
              ADD CN-1           TO WK-ESCRITOS2
              INITIALIZE CPYSAL2
           ELSE
              SET SI-ERROR       TO TRUE

              PERFORM 3000-FIN
                 THRU 3000-FIN-EXIT
           END-IF
      *
           .
      *
       2450-ESCRIBIR-SALIDA2-EXIT.
           EXIT.
      ******************************************************************
      *   3000-FIN
      ******************************************************************
       3000-FIN.
      *
           PERFORM 3100-CERRAR-FICHEROS
              THRU 3100-CERRAR-FICHEROS-EXIT
      *
           IF NO-ERROR
              PERFORM 3200-GRABAR-ESTADIS
                 THRU 3200-GRABAR-ESTADIS-EXIT
           ELSE
              PERFORM 3300-GRABAR-ERROR
                 THRU 3300-GRABAR-ERROR-EXIT
           END-IF
      *
           STOP RUN
      *
           .
      *
       3000-FIN-EXIT.
           EXIT.
      ******************************************************************
      *   3100-CERRAR-FICHEROS
      ******************************************************************
       3100-CERRAR-FICHEROS.
      *
           CLOSE FENTRADA
                 FENTRADA2
                 FSALIDA
                 FSALIDA2
      *
           IF FS-FENTRADA NOT = CA-00
              SET SI-ERROR         TO TRUE
           END-IF
      *
           IF FS-FENTRADA2 NOT = CA-00
              SET SI-ERROR         TO TRUE
           END-IF
      *
           IF FS-FSALIDA NOT = CA-00
              SET SI-ERROR         TO TRUE
           END-IF
      *
           IF FS-FSALIDA2 NOT = CA-00
              SET SI-ERROR         TO TRUE
           END-IF
      *
           .
      *
       3100-CERRAR-FICHEROS-EXIT.
           EXIT.
      ******************************************************************
      *   3200-GRABAR-ESTADIS
      ******************************************************************
       3200-GRABAR-ESTADIS.
      *
           DISPLAY '**************************************'
           DISPLAY '*     ESTADISTICAS SALIDA            *'
           DISPLAY '* REGISTROS LEIDOS   FE:     *' WK-LEIDOS
           DISPLAY '* REGISTROS LEIDOS   FE:     *' WK-LEIDOS2
           DISPLAY '* REGISTROS ESCRITOS FS:     *' WK-ESCRITOS
           DISPLAY '* REGISTROS ESCRITOS FS:     *' WK-ESCRITOS2
           DISPLAY '**************************************'
      *
           .
      *
       3200-GRABAR-ESTADIS-EXIT.
           EXIT.
      ******************************************************************
      *   3300-GRABAR-ERROR
      ******************************************************************
       3300-GRABAR-ERROR.
      *
           DISPLAY '**************************************'
           DISPLAY '*    SE HA PRODUCIDO UN ERROR        *'
           DISPLAY '* FILE STATUS DEL    FE:     *' FS-FENTRADA
           DISPLAY '* FILE STATUS DEL    FE:     *' FS-FENTRADA2
           DISPLAY '* FILE STATUS DEL    FS:     *' FS-FSALIDA
           DISPLAY '* FILE STATUS DEL    FS:     *' FS-FSALIDA2
           DISPLAY '**************************************'
      *
           .
      *
       3300-GRABAR-ERROR-EXIT.
           EXIT.
      ******************************************************************
      *   9000-LEER-ENTRADA
      ******************************************************************
       9000-LEER-ENTRADA-COMP.
      *
           INITIALIZE CPYENT1
      *
           READ FENTRADA INTO CPYENT1
              AT END
                 SET SI-FIN-FICHERO1 TO TRUE
           END-READ
      *
           IF FS-FENTRADA = CA-00
              ADD CN-1               TO WK-LEIDOS
           ELSE
              IF FS-FENTRADA = CA-10
                 DISPLAY 'HE LLEGADO AL FINAL DEL FICHERO'
              ELSE
                 SET SI-ERROR        TO TRUE

                 PERFORM 3000-FIN
                    THRU 3000-FIN-EXIT
              END-IF
           END-IF
           .
      *
       9000-LEER-ENTRADA-COMP-EXIT.
           EXIT.
      ******************************************************************
       9100-LEER-ENTRADA-SEG.
      *
           INITIALIZE CPYENT2
      *
           READ FENTRADA2 INTO CPYENT2
              AT END
                 SET SI-FIN-FICHERO2 TO TRUE
           END-READ
      *
           IF FS-FENTRADA2 = CA-00
              ADD CN-1               TO WK-LEIDOS2
           ELSE
              IF FS-FENTRADA2 = CA-10
                 DISPLAY 'HE LLEGADO AL FINAL DEL FICHERO'
              ELSE
                 SET SI-ERROR        TO TRUE

                 PERFORM 3000-FIN
                    THRU 3000-FIN-EXIT
              END-IF
           END-IF
           .
      *
       9100-LEER-ENTRADA-SEG-EXIT.
           EXIT.

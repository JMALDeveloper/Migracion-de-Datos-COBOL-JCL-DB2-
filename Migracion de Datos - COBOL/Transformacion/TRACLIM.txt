      ******************************************************************
      *       I D E N T I F I C A T I O N   D I V I S I O N
      *
      * PROGRAMA PARA TRANSFORMACION DE FICHERO:
      * DES.VAL.SEG.NMAPFRE.F280519 (SEGURO_PEPITO_SEG)
      *
      * TRANSFORMACION A 3 FORMATOS:
      * DES.TRANS.VIDA.MAP.F300519 (VIDA MAPFRE)
      * DES.TRANS.HOGAR.MAP.F300519 (HOGAR MAPFRE)
      * DES.TRANS.AUTO.MAP.F300519 (AUTO MAPFRE)
      * ****************************************************************
      * RUTINA DE CONTRAVALORACION AÑADIDIDA 'RUTCONT'*****************
      ******************************************************************
       IDENTIFICATION DIVISION.
      *
       PROGRAM-ID.    TRACLIM.
       AUTHOR.        JOAQUIN ALPANYEZ LOPEZ.
       DATE-WRITTEN.  18/09/2023.
       DATE-COMPILED. 18/09/2023.
      *
      ******************************************************************
      *       E N V I R O N M E N T     D I V I S I O N
      ******************************************************************
       ENVIRONMENT DIVISION.
      *
       CONFIGURATION SECTION.
         SOURCE-COMPUTER.  IBM-3090.
         OBJECT-COMPUTER.  IBM-3090.
      *
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT FENTRADA
               ASSIGN TO ENTRADA
               FILE STATUS IS FS-FENTRADA.
      *
           SELECT FSALIDA
               ASSIGN TO SALIDA
               FILE STATUS IS FS-FSALIDA.
      *
           SELECT FSALIDA2
               ASSIGN TO SALIDA2
               FILE STATUS IS FS-FSALIDA2.
      *
           SELECT FSALIDA3
               ASSIGN TO SALIDA3
               FILE STATUS IS FS-FSALIDA3.
      *
           SELECT FSALIDA4
               ASSIGN TO SALIDA4
               FILE STATUS IS FS-FSALIDA4.
      ******************************************************************
      *                D A T A      D I V I S I O N
      ******************************************************************
       DATA DIVISION.
      *
       FILE SECTION.
      *
      ******************************************************************
      *        F I L E     S E C T I O N
      ******************************************************************
      *
       FD  FENTRADA
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORD ARE STANDARD
           RECORDING MODE IS F
           DATA RECORD IS REG-ENT.

       01  REG-ENT            PIC X(614).
      *
       FD  FSALIDA
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORD ARE STANDARD
           RECORDING MODE IS F
           DATA RECORD IS REG-SAL.

       01  REG-SAL            PIC X(310).
      *
       FD  FSALIDA2
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORD ARE STANDARD
           RECORDING MODE IS F
           DATA RECORD IS REG-SAL2.

       01  REG-SAL2           PIC X(335).
      *
       FD  FSALIDA3
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORD ARE STANDARD
           RECORDING MODE IS F
           DATA RECORD IS REG-SAL3.
       01  REG-SAL3           PIC X(340).
      *
       FD  FSALIDA4
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORD ARE STANDARD
           RECORDING MODE IS F
           DATA RECORD IS REG-SAL4.
       01  REG-SAL4           PIC X(20).
      ******************************************************************
      *        W O R K I N G    S T O R A G E
      ******************************************************************
       WORKING-STORAGE SECTION.
      **************************-CONSTANTES-****************************
       01 CA-CONSTANTES-ALF.
          05 CA-PGM                       PIC X(08) VALUE 'TRACLIM'.
          05 CA-RUT                       PIC X(08) VALUE 'RUTCONT'.
          05 CA-00                        PIC X(02) VALUE '00'.
          05 CA-10                        PIC X(02) VALUE '10'.
      *
       01 CN-CONSTANTES-NUM.
          05 CN-1                         PIC 9(01) VALUE 1.
      ************************-VARIABLES-*******************************
       01 WK-VARIABLES.
          05 WK-LEIDOS                    PIC 9(03) VALUE 000.
          05 WK-ESCRITOS                  PIC 9(03) VALUE 000.
          05 WK-ESCRITOS2                 PIC 9(03) VALUE 000.
          05 WK-ESCRITOS3                 PIC 9(03) VALUE 000.
          05 WK-ESCRITOS4                 PIC 9(03) VALUE 000.
      *
       01 FS-FILE-STATUS.
          05 FS-FENTRADA                  PIC X(02).
          05 FS-FSALIDA                   PIC X(02).
          05 FS-FSALIDA2                  PIC X(02).
          05 FS-FSALIDA3                  PIC X(02).
          05 FS-FSALIDA4                  PIC X(02).
      *
      ********************-ESTRUCTURAS-*********************************
       01 CPYENT1.
          05 ID-E1                   PIC S9(9).
          05 NUMERO-POLIZA-E1        PIC X(09).
          05 NOMBRE-COMPANYA-E1      PIC X(55).
          05 TIPO-E1                 PIC X(02).
          05 FECHA-INICIO-E1         PIC X(10).
          05 FECHA-VENCIMIENTO-E1    PIC X(10).
          05 COND-PART-E1            PIC X(255).
          05 OBSERVACIONES-E1        PIC X(255).
          05 DNI-CL-E1               PIC X(09).
      *
       01 CPYSAL1.
          05 POLIZA-S1               PIC X(09).
          05 PRIMA-S1                PIC 9(15)V9(02).
          05 EDAD-S1                 PIC 9(9).
          05 COBERTURAS-S1           PIC X(255).
          05 FECHA-INICIO-S1         PIC X(10).
          05 FECHA-VENCIMIENTO-S1    PIC X(10).
      *
       01 CPYSAL2.
          05 POLIZA-S2               PIC X(09).
          05 PRIMA-S2                PIC 9(15)V9(02).
          05 CONTINENTE-S2           PIC 9(15)V9(02).
          05 CONTENIDO-S2            PIC 9(15)V9(02).
          05 COBERTURAS-S2           PIC X(255).
          05 FECHA-INICIO-S2         PIC X(10).
          05 FECHA-VENCIMIENTO-S2    PIC X(10).
      *
       01 CPYSAL3.
          05 POLIZA-S3               PIC X(09).
          05 PRIMA-S3                PIC 9(15)V9(02).
          05 EDAD-S3                 PIC 9(9).
          05 CATEGORIA-S3            PIC X(30).
          05 COBERTURAS-S3           PIC X(255).
          05 FECHA-INICIO-S3         PIC X(10).
          05 FECHA-VENCIMIENTO-S3    PIC X(10).
      ******************ESTRUCTURA COMUNICACION RUTINA******************
       01 CPYCOMCONT.
          05 ENTRADA.
             10 IMPORT-ORIG               PIC 9(15)V9(02).
             10 DIV-ORIG                  PIC X(03).
          05 SALIDA.
             10 IMPORT-DEST               PIC 9(15)V9(02).
             10 DIV-DEST                  PIC X(03).
          05 ERRORES.
             10 RETORNO                   PIC X(02).
             10 SUBRETORNO                PIC X(02).
             10 TABLA                     PIC X(20).
             10 PARRAFO                   PIC X(30).
             10 ACCION                    PIC X(10).
             10 SQLCODE                   PIC S9(04) COMP.
             10 RUTINA                    PIC X(08).
      ***********************-COMODINES-********************************
       01 COMVIDA.
          05 WK-COMODIN1-COM1                  PIC X(05).
          05 PRIMA-COM1                        PIC X(20).
          05 PRIMA1-COM1                       PIC 9(15)V9(02).
          05 PRIMA2-COM1                       PIC 9(15)V9(02).
          05 WK-COMODIN2-COM1                  PIC X(30).
          05 WK-COMODIN22-COM1                 PIC X(30).
          05 EDAD-COM1                         PIC 9(09).
          05 WK-COMODIN3-COM1                  PIC X(11).
          05 COBERTURAS1-COM1                  PIC X(60).
          05 COBERTURAS2-COM1                  PIC X(60).
          05 COBERTURAS3-COM1                  PIC X(60).
          05 WK-COBERTURAS-TOT1-COM1           PIC X(255).
      *
       01 COMHOGAR.
          05 WK-COMODIN1-COM2                  PIC X(20).
          05 PRIMA-COM2                        PIC X(20).
          05 PRIMA1-COM2                       PIC 9(15)V9(02).
          05 PRIMA11-COM2                      PIC 9(15)V9(02).
          05 WK-COMODIN2-COM2                  PIC X(20).
          05 WK-COMODIN22-COM2                 PIC X(20).
          05 CONTINENTE-COM2                   PIC X(20).
          05 CONTINENTE2-COM2                  PIC 9(15)V9(02).
          05 CONTINENTE3-COM2                  PIC 9(15)V9(02).
          05 CONTINENTE4-COM2                  PIC 9(15).
          05 WK-COMODIN3-COM2                  PIC X(30).
          05 WK-COMODIN33-COM2                 PIC X(30).
          05 CONTENIDO-COM2                    PIC X(20).
          05 CONTENIDO2-COM2                   PIC 9(15)V9(02).
          05 CONTENIDO3-COM2                   PIC 9(15)V9(02).
          05 WK-COMODIN4-COM2                  PIC X(60).
          05 WK-COMODIN44-COM2                 PIC X(60).
          05 COBERTURAS1-COM2                  PIC X(60).
          05 COBERTURAS2-COM2                  PIC X(60).
          05 COBERTURAS3-COM2                  PIC X(60).
          05 COBERTURAS4-COM2                  PIC X(60).
          05 WK-COBERTURAS-TOT2-COM2           PIC X(255).
      *
       01 COMAUTO.
          05 WK-COMODIN1-COM3                  PIC X(05).
          05 PRIMA-COM3                        PIC X(20).
          05 PRIMA1-COM3                       PIC 9(15)V9(02).
          05 PRIMA2-COM3                       PIC 9(15)V9(02).
          05 WK-COMODIN2-COM3                  PIC X(30).
          05 WK-COMODIN22-COM3                 PIC X(30).
          05 EDAD-COM3                         PIC 9(09).
          05 WK-COMODIN3-COM3                  PIC X(30).
          05 WK-COMODIN33-COM3                 PIC X(30).
          05 CATEGORIA-COM3                    PIC X(30).
          05 COBERTURAS1-COM3                  PIC X(60).
          05 COBERTURAS2-COM3                  PIC X(60).
          05 COBERTURAS3-COM3                  PIC X(60).
          05 COBERTURAS4-COM3                  PIC X(60).
          05 WK-COBERTURAS-TOT3-COM3           PIC X(255).
      *
       01 RUT-COM.
          05 COM1                              PIC X(03).
          05 COM2                              PIC X(03).
          05 COM3                              PIC X(03).
          05 COM4                              PIC X(01).
      ************************-SWITCHES-********************************
       01 SWITCHES.
          05 SW-ERROR                     PIC X(01) VALUE 'N'.
             88 SI-ERROR                            VALUE 'S'.
             88 NO-ERROR                            VALUE 'N'.
      *
          05 SW-FIN-FICHERO               PIC X(01) VALUE 'N'.
             88 SI-FIN-FICHERO                      VALUE 'S'.
             88 NO-FIN-FICHERO                      VALUE 'N'.
      *
      ******************************************************************
      *        P R O C E D U R E     D I V I S I O N
      ******************************************************************
       PROCEDURE DIVISION.
      *
           PERFORM 1000-INICIO
              THRU 1000-INICIO-EXIT
      *
           PERFORM 2000-PROCESO
              THRU 2000-PROCESO-EXIT
             UNTIL SI-FIN-FICHERO
      *
           PERFORM 3000-FIN
              THRU 3000-FIN-EXIT.
      *
      ******************************************************************
      *   1000-INICIO
      ******************************************************************
       1000-INICIO.
      *
           INITIALIZE WK-VARIABLES
                      FS-FILE-STATUS
                      CPYENT1
                      CPYSAL1
                      CPYSAL2
                      CPYSAL3
                      CPYCOMCONT
      *
           SET NO-ERROR              TO TRUE
           SET NO-FIN-FICHERO        TO TRUE
      *
           PERFORM 1050-ABRIR-ENTRADA
              THRU 1050-ABRIR-ENTRADA-EXIT
      *
           PERFORM 1100-ABRIR-SALIDA
              THRU 1100-ABRIR-SALIDA-EXIT
      *
           PERFORM 9000-LEER-ENTRADA
              THRU 9000-LEER-ENTRADA-EXIT
      *
           .
       1000-INICIO-EXIT.
           EXIT.
      ******************************************************************
      *   1050-ABRIR-ENTRADA
      ******************************************************************
       1050-ABRIR-ENTRADA.
      *
           OPEN INPUT FENTRADA
      *
           IF FS-FENTRADA = CA-00
              CONTINUE
           ELSE
              SET SI-ERROR          TO TRUE
      *
              PERFORM 3000-FIN
                 THRU 3000-FIN-EXIT
      *
           END-IF
      *
           .
       1050-ABRIR-ENTRADA-EXIT.
           EXIT.
      ******************************************************************
      *   1100-ABRIR-SALIDA
      ******************************************************************
       1100-ABRIR-SALIDA.
      *
           OPEN OUTPUT FSALIDA
      *
           IF FS-FSALIDA = CA-00
              CONTINUE
           ELSE
              SET SI-ERROR          TO TRUE
      *
              PERFORM 3000-FIN
                 THRU 3000-FIN-EXIT
      *
           END-IF
      *
           OPEN OUTPUT FSALIDA2
      *
           IF FS-FSALIDA2 = CA-00
              CONTINUE
           ELSE
              SET SI-ERROR          TO TRUE
      *
              PERFORM 3000-FIN
                 THRU 3000-FIN-EXIT
      *
           END-IF
      *
           OPEN OUTPUT FSALIDA3
      *
           IF FS-FSALIDA3 = CA-00
              CONTINUE
           ELSE
              SET SI-ERROR          TO TRUE
      *
              PERFORM 3000-FIN
                 THRU 3000-FIN-EXIT
      *
           END-IF
      *
             OPEN OUTPUT FSALIDA4
      *
           IF FS-FSALIDA4 = CA-00
              CONTINUE
           ELSE
              SET SI-ERROR          TO TRUE
      *
              PERFORM 3000-FIN
                 THRU 3000-FIN-EXIT
      *
           END-IF
           .
       1100-ABRIR-SALIDA-EXIT.
           EXIT.
      ******************************************************************
      *   2000-PROCESO
      ******************************************************************
       2000-PROCESO.
      *
               EVALUATE TIPO-E1
                   WHEN '01'
      *
                        PERFORM 2222-STRINGS-VIDA
                           THRU 2222-STRINGS-VIDA-EXIT
      *
                        PERFORM 2500-INFORMAR-SALIDA1
                           THRU 2500-INFORMAR-SALIDA1-EXIT
      *
                        PERFORM 2300-ESCRIBIR-SALIDA
                           THRU 2300-ESCRIBIR-SALIDA-EXIT
      *
                   WHEN '02'
      *
                        PERFORM 2333-STRINGS-HOGAR
                           THRU 2333-STRINGS-HOGAR-EXIT
      *
                        PERFORM 2600-INFORMAR-SALIDA2
                           THRU 2600-INFORMAR-SALIDA2-EXIT
      *
                        PERFORM 2450-ESCRIBIR-SALIDA2
                           THRU 2450-ESCRIBIR-SALIDA2-EXIT
      *
                   WHEN OTHER
      *
                        PERFORM 2444-STRINGS-AUTO
                           THRU 2444-STRINGS-AUTO-EXIT
      *
                        PERFORM 2700-INFORMAR-SALIDA3
                           THRU 2700-INFORMAR-SALIDA3-EXIT
      *
                        PERFORM 2455-ESCRIBIR-SALIDA3
                           THRU 2455-ESCRIBIR-SALIDA3-EXIT
      *
                END-EVALUATE
      *
                PERFORM 2456-STRINGS-RUTINA
                   THRU 2456-STRINGS-RUTINA-EXIT
      *
                PERFORM 2666-LLAMADA-RUTINA
                   THRU 2666-LLAMADA-RUTINA-EXIT
      *
                PERFORM 2555-ESCRIBIR-SALIDA4
                   THRU 2555-ESCRIBIR-SALIDA4-EXIT
      *
                PERFORM 9000-LEER-ENTRADA
                   THRU 9000-LEER-ENTRADA-EXIT
      *
           .
       2000-PROCESO-EXIT.
           EXIT.
      ******************************************************************
       2222-STRINGS-VIDA.
      *
               UNSTRING COND-PART-E1
                  DELIMITED BY ALL ':' OR ALL ',' OR ALL 'EUR'
                                       OR ALL 'EDAD: '
                      INTO WK-COMODIN1-COM1
                           PRIMA-COM1
                           WK-COMODIN2-COM1
                           WK-COMODIN22-COM1
                           EDAD-COM1
                           WK-COMODIN3-COM1
                           COBERTURAS1-COM1
                           COBERTURAS2-COM1
                           COBERTURAS3-COM1
      *
               STRING COBERTURAS1-COM1
                      COBERTURAS2-COM1
                      COBERTURAS3-COM1
               DELIMITED BY SIZE INTO WK-COBERTURAS-TOT1-COM1
      *
               UNSTRING PRIMA-COM1 DELIMITED BY ALL ' '
               INTO     PRIMA1-COM1
                        PRIMA2-COM1
      *
             .
       2222-STRINGS-VIDA-EXIT.
               EXIT.
      ******************************************************************
       2333-STRINGS-HOGAR.
      *
               UNSTRING COND-PART-E1
                  DELIMITED BY ALL ':' OR ALL ',' OR ALL 'EUR'
                      INTO WK-COMODIN1-COM2
                           PRIMA-COM2
                           WK-COMODIN2-COM2
                           WK-COMODIN22-COM2
                           CONTINENTE-COM2
                           WK-COMODIN3-COM2
                           WK-COMODIN33-COM2
                           CONTENIDO-COM2
                           WK-COMODIN4-COM2
                           WK-COMODIN44-COM2
                           COBERTURAS1-COM2
                           COBERTURAS2-COM2
                           COBERTURAS3-COM2
                           COBERTURAS4-COM2
      *
               STRING COBERTURAS1-COM2
                      COBERTURAS2-COM2
                      COBERTURAS3-COM2
                      COBERTURAS4-COM2
               DELIMITED BY SIZE INTO WK-COBERTURAS-TOT2-COM2
      *
               UNSTRING PRIMA-COM2 DELIMITED BY ALL ' '
                        INTO PRIMA1-COM2
                             PRIMA11-COM2
      *
               UNSTRING CONTINENTE-COM2 DELIMITED BY ALL ' '
                        INTO CONTINENTE2-COM2
                             CONTINENTE3-COM2
      *
               UNSTRING CONTENIDO-COM2 DELIMITED BY ALL ' '
                        INTO CONTENIDO2-COM2
                             CONTENIDO3-COM2
      *
             .
       2333-STRINGS-HOGAR-EXIT.
               EXIT.
      ******************************************************************
       2444-STRINGS-AUTO.
      *
               UNSTRING COND-PART-E1
                  DELIMITED BY ALL ':' OR ALL ',' OR ALL 'EUR'
                                       OR ALL 'EDAD: '
                      INTO WK-COMODIN1-COM3
                           PRIMA-COM3
                           WK-COMODIN2-COM3
                           WK-COMODIN22-COM3
                           EDAD-COM3
                           WK-COMODIN3-COM3
                           CATEGORIA-COM3
                           COBERTURAS1-COM3
                           COBERTURAS2-COM3
                           COBERTURAS3-COM3
                           COBERTURAS4-COM3
      *
               STRING COBERTURAS1-COM3
                      COBERTURAS2-COM3
                      COBERTURAS3-COM3
                      COBERTURAS4-COM3
               DELIMITED BY SIZE INTO WK-COBERTURAS-TOT3-COM3
      *
               UNSTRING PRIMA-COM3 DELIMITED BY ALL ' '
                        INTO PRIMA1-COM3
                             PRIMA2-COM3
      *
             .
       2444-STRINGS-AUTO-EXIT.
               EXIT.
      ******************************************************************
       2500-INFORMAR-SALIDA1.
      *
                   MOVE NUMERO-POLIZA-E1        TO POLIZA-S1
                   MOVE PRIMA2-COM1             TO PRIMA-S1
                   MOVE EDAD-COM1               TO EDAD-S1
                   MOVE WK-COBERTURAS-TOT1-COM1 TO COBERTURAS-S1
                   MOVE FECHA-INICIO-E1         TO FECHA-INICIO-S1
                   MOVE FECHA-VENCIMIENTO-E1    TO FECHA-VENCIMIENTO-S1
      *
               .
       2500-INFORMAR-SALIDA1-EXIT.
                EXIT.
      ******************************************************************
       2600-INFORMAR-SALIDA2.
      *

               MOVE NUMERO-POLIZA-E1         TO POLIZA-S2
               MOVE PRIMA11-COM2             TO PRIMA-S2
               MOVE CONTINENTE3-COM2         TO CONTINENTE-S2
               MOVE CONTENIDO3-COM2          TO CONTENIDO-S2
               MOVE WK-COBERTURAS-TOT2-COM2  TO COBERTURAS-S2
               MOVE FECHA-INICIO-E1          TO FECHA-INICIO-S2
               MOVE FECHA-VENCIMIENTO-E1     TO FECHA-VENCIMIENTO-S2
      *
               .
       2600-INFORMAR-SALIDA2-EXIT.
                EXIT.
      ******************************************************************
       2700-INFORMAR-SALIDA3.
      *
               MOVE NUMERO-POLIZA-E1         TO POLIZA-S3
               MOVE PRIMA2-COM3              TO PRIMA-S3
               MOVE EDAD-COM3                TO EDAD-S3
               MOVE CATEGORIA-COM3           TO CATEGORIA-S3
               MOVE WK-COBERTURAS-TOT3-COM3  TO COBERTURAS-S3
               MOVE FECHA-INICIO-E1          TO FECHA-INICIO-S3
               MOVE FECHA-VENCIMIENTO-E1     TO FECHA-VENCIMIENTO-S3
      *
               .
       2700-INFORMAR-SALIDA3-EXIT.
                EXIT.
      ******************************************************************
      *   2300-ESCRIBIR-SALIDA
      ******************************************************************
       2300-ESCRIBIR-SALIDA.
      *
           WRITE REG-SAL FROM CPYSAL1
      *
           IF FS-FSALIDA = CA-00
              ADD CN-1           TO WK-ESCRITOS
              INITIALIZE CPYSAL1
           ELSE
              SET SI-ERROR       TO TRUE

              PERFORM 3000-FIN
                 THRU 3000-FIN-EXIT
           END-IF
      *
           .
      *
       2300-ESCRIBIR-SALIDA-EXIT.
           EXIT.
      ******************************************************************
       2450-ESCRIBIR-SALIDA2.
      *
           WRITE REG-SAL2 FROM CPYSAL2
      *
           IF FS-FSALIDA2 = CA-00
              ADD CN-1           TO WK-ESCRITOS2
              INITIALIZE CPYSAL2
           ELSE
              SET SI-ERROR       TO TRUE

              PERFORM 3000-FIN
                 THRU 3000-FIN-EXIT
           END-IF
      *
           .
      *
       2450-ESCRIBIR-SALIDA2-EXIT.
           EXIT.
      ******************************************************************
       2455-ESCRIBIR-SALIDA3.
      *
           WRITE REG-SAL3 FROM CPYSAL3
      *
           IF FS-FSALIDA3 = CA-00
              ADD CN-1           TO WK-ESCRITOS3
              INITIALIZE CPYSAL3
           ELSE
              SET SI-ERROR       TO TRUE

              PERFORM 3000-FIN
                 THRU 3000-FIN-EXIT
           END-IF
      *
           .
      *
       2455-ESCRIBIR-SALIDA3-EXIT.
           EXIT.
      ******************************************************************
       2555-ESCRIBIR-SALIDA4.
      *
           WRITE REG-SAL4 FROM SALIDA
      *
           IF FS-FSALIDA4 = CA-00
              ADD CN-1           TO WK-ESCRITOS4
              INITIALIZE SALIDA
           ELSE
              SET SI-ERROR       TO TRUE

              PERFORM 3000-FIN
                 THRU 3000-FIN-EXIT
           END-IF
      *
           .
      *
       2555-ESCRIBIR-SALIDA4-EXIT.
           EXIT.
      ******************************************************************
       2666-LLAMADA-RUTINA.
      *
                CALL CA-RUT USING CPYCOMCONT
      *
               .
       2666-LLAMADA-RUTINA-EXIT.
              EXIT.
      ******************************************************************
       2456-STRINGS-RUTINA.
      *
                UNSTRING COND-PART-E1
                DELIMITED BY 'PRIMA: ' OR ' ' OR ','
                INTO COM1
                     COM2
                     COM3
                     COM4
      *
                MOVE COM2 TO IMPORT-ORIG
                MOVE COM3 TO DIV-ORIG
      *
                  .
       2456-STRINGS-RUTINA-EXIT.
                EXIT.
      ******************************************************************
      *   3000-FIN
      ******************************************************************
       3000-FIN.
      *
           PERFORM 3100-CERRAR-FICHEROS
              THRU 3100-CERRAR-FICHEROS-EXIT
      *
           IF NO-ERROR
              PERFORM 3200-GRABAR-ESTADIS
                 THRU 3200-GRABAR-ESTADIS-EXIT
           ELSE
              PERFORM 3300-GRABAR-ERROR
                 THRU 3300-GRABAR-ERROR-EXIT
           END-IF
      *
           STOP RUN
      *
           .
      *
       3000-FIN-EXIT.
           EXIT.
      ******************************************************************
      *   3100-CERRAR-FICHEROS
      ******************************************************************
       3100-CERRAR-FICHEROS.
      *
           CLOSE FENTRADA
                 FSALIDA
                 FSALIDA2
                 FSALIDA3
                 FSALIDA4
      *
           IF FS-FENTRADA NOT = CA-00
              SET SI-ERROR         TO TRUE
           END-IF
      *
           IF FS-FSALIDA   NOT = CA-00
              SET SI-ERROR         TO TRUE
           END-IF
      *
           IF FS-FSALIDA2 NOT = CA-00
              SET SI-ERROR         TO TRUE
           END-IF
      *
           IF FS-FSALIDA3 NOT = CA-00
              SET SI-ERROR         TO TRUE
           END-IF
      *
           IF FS-FSALIDA4 NOT = CA-00
              SET SI-ERROR         TO TRUE
           END-IF
           .
      *
       3100-CERRAR-FICHEROS-EXIT.
           EXIT.
      ******************************************************************
      *   3200-GRABAR-ESTADIS
      ******************************************************************
       3200-GRABAR-ESTADIS.
      *
           DISPLAY '**************************************'
           DISPLAY '*     ESTADISTICAS SALIDA            *'
           DISPLAY '* REGISTROS LEIDOS   FE:     *' WK-LEIDOS
           DISPLAY '* REGISTROS ESCRITOS FS:     *' WK-ESCRITOS
           DISPLAY '* REGISTROS ESCRITOS FS:     *' WK-ESCRITOS2
           DISPLAY '* REGISTROS ESCRITOS FS:     *' WK-ESCRITOS3
           DISPLAY '* REGISTROS ESCRITOS FS:     *' WK-ESCRITOS4
           DISPLAY '**************************************'
      *
           .
      *
       3200-GRABAR-ESTADIS-EXIT.
           EXIT.
      ******************************************************************
      *   3300-GRABAR-ERROR
      ******************************************************************
       3300-GRABAR-ERROR.
      *
           DISPLAY '**************************************'
           DISPLAY '*    SE HA PRODUCIDO UN ERROR        *'
           DISPLAY '* FILE STATUS DEL    FE:     *' FS-FENTRADA
           DISPLAY '* FILE STATUS DEL    FS:     *' FS-FSALIDA
           DISPLAY '* FILE STATUS DEL    FS:     *' FS-FSALIDA2
           DISPLAY '* FILE STATUS DEL    FS:     *' FS-FSALIDA3
           DISPLAY '* FILE STATUS DEL    FS:     *' FS-FSALIDA4
           DISPLAY '**************************************'
      *
           .
      *
       3300-GRABAR-ERROR-EXIT.
           EXIT.
      ******************************************************************
      *   9000-LEER-ENTRADA
      ******************************************************************
       9000-LEER-ENTRADA.
      *
           INITIALIZE CPYENT1
      *
           READ FENTRADA INTO CPYENT1
              AT END
                 SET SI-FIN-FICHERO  TO TRUE
           END-READ
      *
           IF FS-FENTRADA = CA-00
              ADD CN-1               TO WK-LEIDOS
           ELSE
              IF FS-FENTRADA = CA-10
                 DISPLAY 'HE LLEGADO AL FINAL DEL FICHERO'
              ELSE
                 SET SI-ERROR        TO TRUE

                 PERFORM 3000-FIN
                    THRU 3000-FIN-EXIT
              END-IF
           END-IF
           .
      *
       9000-LEER-ENTRADA-EXIT.
           EXIT.
      ******************************************************************

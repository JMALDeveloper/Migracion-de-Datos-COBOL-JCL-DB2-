      ******************************************************************
      *       I D E N T I FI C A T I O N   D I V I S I O N
      *
      * PROGRAMA DE CARGA DEL FICHERO DES.TRANS.AUTO.MAP.300519
      * REPOSICIONAMIENTO INCLUIDO
      * FICHERO INCIDENCIAS (DES.INC.AUTO.MAP.030619) INCLUIDO
      ******************************************************************
       IDENTIFICATION DIVISION.
      *
       PROGRAM-ID.    LOADAUT.
       AUTHOR.        JOAQUIN ALPANYEZ LOPEZ.
       DATE-WRITTEN.  21/09/2023.
       DATE-COMPILED. 21/09/2023.
      *
      ******************************************************************
      *       E N V I R O N M E N T     D I V I S I O N
      ******************************************************************
       ENVIRONMENT DIVISION.
      *
       CONFIGURATION SECTION.
         SOURCE-COMPUTER.  IBM-3090.
         OBJECT-COMPUTER.  IBM-3090.
      *
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT FENTRADA
               ASSIGN TO ENTRADA
               FILE STATUS IS FS-FENTRADA.
      *
           SELECT FSALIDA
               ASSIGN TO SALIDA
               FILE STATUS IS FS-FSALIDA.
      ******************************************************************
      *                D A T A      D I V I S I O N
      ******************************************************************
       DATA DIVISION.
      *
       FILE SECTION.
      *
      ******************************************************************
      *        F I L E     S E C T I O N
      ******************************************************************
      *
       FD  FENTRADA
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORD ARE STANDARD
           RECORDING MODE IS F
           DATA RECORD IS REG-ENT.

       01  REG-ENT            PIC X(340).
      *
       FD  FSALIDA
           BLOCK CONTAINS 0 RECORDS
           LABEL RECORD ARE STANDARD
           RECORDING MODE IS F
           DATA RECORD IS REG-SAL.

       01  REG-SAL            PIC X(100).
      ******************************************************************
      *        W O R K I N G    S T O R A G E
      ******************************************************************
       WORKING-STORAGE SECTION.
      *************************-CONSTANTES-*****************************
       01 CA-CONSTANTES-ALF.
          05 CA-PGM                       PIC X(08) VALUE 'LOADAUT'.
          05 CA-00                        PIC X(02) VALUE '00'.
          05 CA-10                        PIC X(02) VALUE '10'.
          05 CA-OK                        PIC X(02) VALUE 'OK'.
          05 CA-KO                        PIC X(02) VALUE 'KO'.
      *************************-VARIABLES-******************************
       01 FS-FILESTATUS.
          05 FS-FENTRADA                  PIC X(02).
          05 FS-FSALIDA                   PIC X(02).
      *
       01 WK-SQLCODE                      PIC -999.
      *
       01 WK-CLAVE.
          05 POLIZA-CLAVE                 PIC X(09).
      *************************-ESTRUCTURAS-****************************
       01 CPYENT.
          05 POLIZA-E                PIC X(09).
          05 PRIMA-E                 PIC 9(15)V9(02).
          05 EDAD-E                  PIC 9(9).
          05 CATEGORIA-E             PIC X(30).
          05 COBERTURAS-E            PIC X(255).
          05 FECHA-INICIO-E          PIC X(10).
          05 FECHA-VENCIMIENTO-E     PIC X(10).
      *
       01 CPYSAL.
          05 RETORNO                     PIC X(02).
          05 DESCRIPCION                 PIC X(50).
          05 PARRAFO                     PIC X(30).
          05 SQLCODE-S                   PIC -999.
      **********************-SWITCHES-**********************************
       01 SWITCHES.
          05 SW-FIN-FICHERO               PIC X(01).
             88 SI-FIN-FICHERO            VALUE 'S'.
             88 NO-FIN-FICHERO            VALUE 'N'.
      *************************-TABLAS/DB2-*****************************
           EXEC SQL
               INCLUDE SQLCA
           END-EXEC.
      *
           EXEC SQL
               INCLUDE TBAUTOMA
           END-EXEC.
      *
           EXEC SQL
               INCLUDE TBDAREPO
           END-EXEC.
      *
      ******************************************************************
      *        P R O C E D U R E     D I V I S I O N
      ******************************************************************
       PROCEDURE DIVISION.
      *
           PERFORM 1000-INICIO
              THRU 1000-INICIO-EXIT

           PERFORM 2000-PROCESO
              THRU 2000-PROCESO-EXIT
             UNTIL SI-FIN-FICHERO

           PERFORM 3000-FIN
              THRU 3000-FIN-EXIT.

      *
      ******************************************************************
      *   1000-INICIO
      ******************************************************************
       1000-INICIO.
      *
           INITIALIZE DCLAUTO-MAPFRE
                      FS-FILESTATUS
                      DCLDAREPOS
                      CPYENT
                      CPYSAL
      *
           SET NO-FIN-FICHERO  TO TRUE
      *
           PERFORM 1100-ABRIR-FICHEROS
              THRU 1100-ABRIR-FICHEROS-EXIT
      *
           MOVE CA-PGM         TO TB-NOMBRE-PGM
      *
           PERFORM 1200-CONSULTAR-DAREPOS
              THRU 1200-CONSULTAR-DAREPOS-EXIT
      *
           .
       1000-INICIO-EXIT.
           EXIT.
      ******************************************************************
      *   1100-ABRIR-FICHEROS
      ******************************************************************
       1100-ABRIR-FICHEROS.
      *
           OPEN INPUT FENTRADA
      *
           IF FS-FENTRADA = CA-00
              CONTINUE
      *
           ELSE
      *
              PERFORM 3000-FIN
                 THRU 3000-FIN-EXIT
      *
           END-IF
      *
           OPEN OUTPUT FSALIDA
      *
           IF FS-FSALIDA  = CA-00
              CONTINUE
      *
           ELSE
      *
              PERFORM 3000-FIN
                 THRU 3000-FIN-EXIT
      *
           END-IF
      *
           .
       1100-ABRIR-FICHEROS-EXIT.
           EXIT.
      ******************************************************************
      *   1200-CONSULTAR-DAREPOS
      ******************************************************************
       1200-CONSULTAR-DAREPOS.
      *
           EXEC SQL
              SELECT ESTADO
                    ,VALOR_CLAVE
                INTO :TB-ESTADO
                    ,:TB-VALOR-CLAVE-TEXT
                FROM DAREPOS
               WHERE NOMBRE_PGM = :TB-NOMBRE-PGM
           END-EXEC
      *
           EVALUATE SQLCODE
      *
              WHEN 0
      *
                   EVALUATE TB-ESTADO
                      WHEN CA-KO
                           PERFORM 9000-LEER-ENTRADA
                              THRU 9000-LEER-ENTRADA-EXIT
                           PERFORM 9100-INFORMAR-TABLA
                              THRU 9100-INFORMAR-TABLA-EXIT
                             UNTIL WK-CLAVE > TB-VALOR-CLAVE-TEXT
      *
                      WHEN CA-OK
                           PERFORM 9000-LEER-ENTRADA
                              THRU 9000-LEER-ENTRADA-EXIT
      *
                           PERFORM 9100-INFORMAR-TABLA
                              THRU 9100-INFORMAR-TABLA-EXIT
      *
                      WHEN OTHER
      *
                           MOVE '99' TO RETORNO
                           MOVE 'ERROR DESCONOCIDO' TO DESCRIPCION
                           MOVE '1200-CONSULTAR-DAREPOS' TO PARRAFO
                           MOVE SQLCODE TO SQLCODE-S
      *
                           PERFORM 2777-ESCRIBIR-INC
                              THRU 2777-ESCRIBIR-INC-EXIT
      *
                           PERFORM 3000-FIN
                              THRU 3000-FIN-EXIT
      *
                   END-EVALUATE
      *
              WHEN 100
      *
                   PERFORM 2500-INSERTAR-DAREPOS
                      THRU 2500-INSERTAR-DAREPOS-EXIT

                   PERFORM 9000-LEER-ENTRADA
                      THRU 9000-LEER-ENTRADA-EXIT
      *
                   PERFORM 9100-INFORMAR-TABLA
                      THRU 9100-INFORMAR-TABLA-EXIT
      *
              WHEN OTHER
      *
                   DISPLAY 'ERROR SELECT DE DAREPOS'
                   MOVE SQLCODE          TO WK-SQLCODE
                   DISPLAY 'SQLCODE: ' WK-SQLCODE
                   DISPLAY 'PARR: CONSULTAR-DAREPOS'
                   DISPLAY 'TABLA: DAREPOS'
      *
                   MOVE '99' TO RETORNO
                   MOVE 'ERROR SELECT DE DAREPOS' TO DESCRIPCION
                   MOVE '1200-CONSULTAR-DAREPOS' TO PARRAFO
                   MOVE SQLCODE TO SQLCODE-S
      *
                   PERFORM 2777-ESCRIBIR-INC
                      THRU 2777-ESCRIBIR-INC-EXIT
      *
                   PERFORM 3000-FIN
                      THRU 3000-FIN-EXIT
      *
           END-EVALUATE
      *
           .
       1200-CONSULTAR-DAREPOS-EXIT.
           EXIT.
      ******************************************************************
      *   9000-LEER-ENTRADA
      ******************************************************************
       9000-LEER-ENTRADA.
      *
           INITIALIZE CPYENT
      *
           READ FENTRADA INTO CPYENT
             AT END
                SET SI-FIN-FICHERO   TO TRUE
           END-READ
      *
           IF FS-FENTRADA = CA-00
              CONTINUE
      *
           ELSE
      *
              IF FS-FENTRADA = CA-10
                 DISPLAY 'HAS LLEGADO AL FINAL DEL FICHERO'
      *
                 PERFORM 2600-DAREPOS-OK
                    THRU 2600-DAREPOS-OK-EXIT
      *
              ELSE
      *
                 PERFORM 3000-FIN
                    THRU 3000-FIN-EXIT
      *
              END-IF
      *
           END-IF
      *
           .
       9000-LEER-ENTRADA-EXIT.
           EXIT.
      ******************************************************************
       9100-INFORMAR-TABLA.
      *
                 INITIALIZE DCLAUTO-MAPFRE
      *
                 MOVE POLIZA-E            TO TB-POLIZA
                 MOVE PRIMA-E             TO TB-PRIMA
                 MOVE EDAD-E              TO TB-EDAD
                 MOVE CATEGORIA-E         TO TB-CATEGORIA
                 MOVE COBERTURAS-E        TO TB-COBERTURAS
                 MOVE FECHA-INICIO-E      TO TB-FECHA-INICIO
                 MOVE FECHA-VENCIMIENTO-E TO TB-FECHA-VENCIMIENTO
      *
                 MOVE TB-POLIZA           TO POLIZA-CLAVE
            .
       9100-INFORMAR-TABLA-EXIT.
             EXIT.
      ******************************************************************
      *   2000-PROCESO
      ******************************************************************
       2000-PROCESO.
      *
           PERFORM 2300-INSERT
              THRU 2300-INSERT-EXIT
      *
           PERFORM 2400-UPDATE-DAREPOS
              THRU 2400-UPDATE-DAREPOS-EXIT
      *
           PERFORM 9000-LEER-ENTRADA
              THRU 9000-LEER-ENTRADA-EXIT
      *
           PERFORM 9100-INFORMAR-TABLA
              THRU 9100-INFORMAR-TABLA-EXIT
      *
           .
       2000-PROCESO-EXIT.
           EXIT.
      ******************************************************************
       2777-ESCRIBIR-INC.
      *
           WRITE REG-SAL FROM CPYSAL
      *
           IF FS-FSALIDA = CA-00
              INITIALIZE CPYSAL
      *
           ELSE
      *
              PERFORM 3000-FIN
                 THRU 3000-FIN-EXIT
           END-IF
      *
             .
       2777-ESCRIBIR-INC-EXIT.
               EXIT.
      ******************************************************************
      *   2300-INSERT
      ******************************************************************
       2300-INSERT.
      *
           EXEC SQL
              INSERT INTO AUTO_MAPFRE
               VALUES (:TB-POLIZA
                      ,:TB-PRIMA
                      ,:TB-EDAD
                      ,:TB-CATEGORIA
                      ,:TB-COBERTURAS
                      ,:TB-FECHA-INICIO
                      ,:TB-FECHA-VENCIMIENTO)
           END-EXEC.
      *
           EVALUATE SQLCODE
               WHEN 0
                    DISPLAY 'LA POLIZA HA SIDO INSERTADA ' TB-POLIZA
      *
               WHEN -803
                    DISPLAY 'LA POLIZA ESTA DUPLICADA ' TB-POLIZA
      *
                    MOVE '88' TO RETORNO
                    MOVE 'POLIZA DUPLICADA' TO DESCRIPCION
                    MOVE '2300-INSERT' TO PARRAFO
                    MOVE SQLCODE TO SQLCODE-S
      *
                    PERFORM 2777-ESCRIBIR-INC
                       THRU 2777-ESCRIBIR-INC-EXIT
      *
                    PERFORM 3000-FIN
                       THRU 3000-FIN-EXIT

               WHEN OTHER
                    DISPLAY 'ERROR DB2 AL INSERTAR POLIZA'
      *
                    MOVE SQLCODE TO WK-SQLCODE
                    DISPLAY 'SQLCODE: ' WK-SQLCODE
                    MOVE '88' TO RETORNO
                    MOVE 'ERROR DB2 AL INSERTAR POLIZA' TO DESCRIPCION
                    MOVE '2300-INSERT' TO PARRAFO
                    MOVE SQLCODE TO SQLCODE-S
      *
                    PERFORM 2777-ESCRIBIR-INC
                       THRU 2777-ESCRIBIR-INC-EXIT
      *
                    PERFORM 3000-FIN
                       THRU 3000-FIN-EXIT

           END-EVALUATE
      *
           .
       2300-INSERT-EXIT.
           EXIT.
      ******************************************************************
      *   2400-UPDATE-DAREPOS
      ******************************************************************
       2400-UPDATE-DAREPOS.
      *
           MOVE CA-PGM                 TO TB-NOMBRE-PGM
           MOVE WK-CLAVE               TO TB-VALOR-CLAVE-TEXT
           MOVE CA-KO                  TO TB-ESTADO
      *
           EXEC SQL
              UPDATE DAREPOS
                 SET VALOR_CLAVE = :TB-VALOR-CLAVE-TEXT
                    ,ESTADO      = :TB-ESTADO
               WHERE NOMBRE_PGM = :TB-NOMBRE-PGM
           END-EXEC.
      *
           EVALUATE SQLCODE
               WHEN 0
                    CONTINUE
               WHEN OTHER
                    DISPLAY 'ERROR DB2 AL CONFIRMAR POLIZA EN DAREPOS'
      *
                    MOVE SQLCODE TO WK-SQLCODE
                    DISPLAY 'SQLCODE: ' WK-SQLCODE
                    MOVE '99' TO RETORNO
                    MOVE 'ERROR CONFIRMAR POLIZA DAREPOS' TO DESCRIPCION
                    MOVE '2400-UPDATE-DAREPOS' TO PARRAFO
                    MOVE SQLCODE TO SQLCODE-S
      *
                    PERFORM 2777-ESCRIBIR-INC
                       THRU 2777-ESCRIBIR-INC-EXIT
      *
                    PERFORM 3000-FIN
                       THRU 3000-FIN-EXIT
           END-EVALUATE
      *
           EXEC SQL
               COMMIT
           END-EXEC
      *
           .
       2400-UPDATE-DAREPOS-EXIT.
           EXIT.
      ******************************************************************
      *   2500-INSERTAR-DAREPOS
      ******************************************************************
       2500-INSERTAR-DAREPOS.
      *
           MOVE CA-KO               TO TB-ESTADO
           INITIALIZE TB-VALOR-CLAVE-TEXT
      *
           EXEC SQL
              INSERT INTO DAREPOS
               VALUES (:TB-NOMBRE-PGM
                      ,:TB-ESTADO
                      ,:TB-VALOR-CLAVE-TEXT)
           END-EXEC.
      *
           EVALUATE SQLCODE
               WHEN 0
                    CONTINUE
      *
               WHEN -803
                    DISPLAY 'ERROR AL INSERTAR EN DAREPOS POR RG DUP'
                    DISPLAY 'RETORNO: 99'
                    DISPLAY 'PARR: INSERTAR-DAREPOS'
                    DISPLAY 'TABLA: DAREPOS'
      *
                    MOVE '88' TO RETORNO
                    MOVE 'ERROR: REG DUP' TO DESCRIPCION
                    MOVE '2500-INSERTAR-DAREPOS' TO PARRAFO
                    MOVE SQLCODE TO SQLCODE-S
      *
                    PERFORM 2777-ESCRIBIR-INC
                       THRU 2777-ESCRIBIR-INC-EXIT
      *
                    PERFORM 3000-FIN
                       THRU 3000-FIN-EXIT
      *
               WHEN OTHER
                    DISPLAY 'ERROR AL INSERTAR EN DAREPOS'
                    DISPLAY 'RETORNO: 99'
                    DISPLAY 'PARR: INSERTAR-DAREPOS'
                    DISPLAY 'TABLA: DAREPOS'
      *
                    MOVE '99' TO RETORNO
                    MOVE 'ERROR: INSERTAR DAREPOS' TO DESCRIPCION
                    MOVE '2500-INSERTAR-DAREPOS' TO PARRAFO
                    MOVE SQLCODE TO SQLCODE-S
      *
                    PERFORM 2777-ESCRIBIR-INC
                       THRU 2777-ESCRIBIR-INC-EXIT
      *
                    PERFORM 3000-FIN
                       THRU 3000-FIN-EXIT
      *
           END-EVALUATE
      *
           .
       2500-INSERTAR-DAREPOS-EXIT.
           EXIT.
      ******************************************************************
      *   2600-DAREPOS-OK
      ******************************************************************
       2600-DAREPOS-OK.
      *
           MOVE CA-OK                  TO TB-ESTADO
           MOVE CA-PGM                 TO TB-NOMBRE-PGM
           INITIALIZE TB-VALOR-CLAVE-TEXT
      *
           EXEC SQL
              UPDATE DAREPOS
                 SET VALOR_CLAVE = :TB-VALOR-CLAVE-TEXT
                    ,ESTADO      = :TB-ESTADO
               WHERE NOMBRE_PGM = :TB-NOMBRE-PGM
           END-EXEC.
      *
           EVALUATE SQLCODE
               WHEN 0
                    CONTINUE
      *
               WHEN OTHER
                    DISPLAY 'ERROR AL PONER OK EN DAREPOS'
                    MOVE SQLCODE TO WK-SQLCODE
                    DISPLAY 'SQLCODE: ' WK-SQLCODE
      *
                    MOVE '99' TO RETORNO
                    MOVE 'ERROR OK DAREPOS' TO DESCRIPCION
                    MOVE '2600-DAREPOS-OK' TO PARRAFO
                    MOVE SQLCODE TO SQLCODE-S
      *
                    PERFORM 2777-ESCRIBIR-INC
                       THRU 2777-ESCRIBIR-INC-EXIT
      *
                    PERFORM 3000-FIN
                       THRU 3000-FIN-EXIT
      *
           END-EVALUATE
      *
                    EXEC SQL
                       COMMIT
                    END-EXEC
      *
           .
       2600-DAREPOS-OK-EXIT.
           EXIT.
      ******************************************************************
      *   3000-FIN
      ******************************************************************
       3000-FIN.
      *
           PERFORM 3100-CERRAR-FICHEROS
              THRU 3100-CERRAR-FICHEROS-EXIT
      *
           STOP RUN
           .
       3000-FIN-EXIT.
           EXIT.
      ******************************************************************
      *   3100-CERRAR-FICHEROS
      ******************************************************************
       3100-CERRAR-FICHEROS.
      *
           CLOSE FENTRADA
                 FSALIDA
      *
           IF FS-FENTRADA NOT = CA-00
              DISPLAY 'ERROR AL CERRAR FICHERO DE ENTRADA'
           END-IF
      *
           IF FS-FSALIDA  NOT = CA-00
              DISPLAY 'ERROR AL CERRAR FICHERO DE SALIDA'
           END-IF
      *
           .
       3100-CERRAR-FICHEROS-EXIT.
           EXIT.
